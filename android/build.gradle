def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

apply plugin: 'com.android.library'

def test_gradle = "../../common-build-test.gradle"
if (file(test_gradle).exists()) {
   tasks.register("unifiedCoverageReport") {
     // this is just to make sure the test execution will not fail here.
   }
}

android {
    if (project.android.hasProperty("namespace")) {
        sourceSets {
            main {
                manifest.srcFile "src/main/AndroidManifestNew.xml" // no package on it
            }
        }
        namespace "com.scandit.datacapture.flutter.id_voided_detection"
    }
    compileSdkVersion safeExtGet("compileSdkVersion", 33)

    defaultConfig {
        minSdkVersion safeExtGet("minSdkVersion", 23)
        targetSdkVersion safeExtGet("targetSdkVersion", 33)
        versionCode 1
        versionName "1.0"
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    if (findProject(":native:libs:sdc:id-voided-detection-models:id-voided-detection-models-android") != null) {
        api(project(path: ":native:libs:sdc:id-voided-detection-models:id-voided-detection-models-android"))
    } else {
        def sdk_version = "6.28.1"

        println("Version of the native sdk used in this build: ${safeExtGet('global_sdk_version', sdk_version)}")
        api "com.scandit.datacapture:id-voided-detection-models:${safeExtGet('global_sdk_version', sdk_version)}"
    }
}
